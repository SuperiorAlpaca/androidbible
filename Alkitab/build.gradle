plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "kotlin-parcelize"
    id 'com.google.firebase.crashlytics'
    id 'kotlinx-serialization'
}

android {
    signingConfigs {
        release {
            keyAlias System.getenv("SIGN_ALIAS")
            keyPassword System.getenv("SIGN_PASSWORD")
            storeFile file(System.getenv("SIGN_KEYSTORE") != null ? System.getenv("SIGN_KEYSTORE") : "/dev/null")
            storePassword System.getenv("SIGN_PASSWORD")
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId 'yuku.alkitab.debug'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 17000520
        versionName '4.10.0'
        multiDexEnabled true
        // Keep this synced with integrate_translations.sh! Also update pref_language.xml and ConfigurationWrapper!
        resourceConfigurations += ['af', 'bg', 'ceb', 'cs', 'da', 'de', 'el', 'es', 'fr', 'hu', 'in', 'it', 'ja', 'ko', 'lv', 'ms', 'my', 'nl', 'pl', 'pt-rBR', 'pt', 'ro', 'ru', 'th', 'tl', 'tr', 'uk', 'vi', 'zh-rCN', 'zh-rTW']
        buildConfigField 'String', 'SERVER_HOST', "\"$SERVER_HOST\""
        buildConfigField 'String', 'RIBKA_FUNCTIONS_HOST', "\"$RIBKA_FUNCTIONS_HOST\""
    }
    buildTypes {
        debug {
            buildConfigField 'String', 'RIBKA_FUNCTIONS_HOST', "\"$RIBKA_FUNCTIONS_HOST_DEBUG\""
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lint {
        abortOnError false
    }

    flavorDimensions "playStoreApplicationId"

    productFlavors {
        // Use this for development
        plain {
        }

        // The following flavors are for release
        yuku_alkitab {
            applicationId 'yuku.alkitab'
        }
        yuku_quick_bible {
            applicationId 'yuku.alkitab.kjv'
        }
        sabda_alkitab {
            applicationId 'org.sabda.alkitab'
        }
    }
    namespace 'yuku.alkitab.debug'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation project(':AlkitabFeedback')
    implementation project(':AlkitabIntegration')
    implementation project(':AlkitabIo')
    implementation project(':AlkitabModel')
    implementation project(':AlkitabYes2')
    implementation project(':BiblePlus')
    implementation project(':DragSortListView')
    implementation project(':KpriModel')
    implementation project(':BintexReader')
    implementation project(':BintexWriter')
    implementation project(':Afw')
    implementation project(':FlowLayout')
    implementation project(':AmbilWarna')
    implementation project(':ATree')
    implementation project(':ImportedDesktopVerseUtil')
    implementation project(':PrDownloaderFixed')

    // Android X
    implementation "androidx.activity:activity-ktx:$androidxActivityVersion"
    implementation "androidx.annotation:annotation:$androidxAnnotationVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintlayoutVersion"
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.fragment:fragment-ktx:$androidxFragmentVersion"
    implementation "androidx.multidex:multidex:$androidxMultidexVersion"
    implementation "androidx.percentlayout:percentlayout:$androidxPercentlayoutVersion"
    implementation "androidx.preference:preference-ktx:$androidxPreferenceVersion"
    implementation "androidx.recyclerview:recyclerview:$androidxRecyclerviewVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$androidxSwiperefreshlayoutVersion"

    // Google
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayerVersion"
    implementation "com.google.android.exoplayer:extension-okhttp:$exoplayerVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "me.zhanghai.android.fastscroll:library:$fastscrollVersion"

    // Tests
    testImplementation "junit:junit:$junitVersion"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:29.0.3')
    implementation "com.google.firebase:firebase-core"
    implementation "com.google.firebase:firebase-messaging"
    implementation "com.google.firebase:firebase-crashlytics"

    // Misc
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    implementation "io.coil-kt:coil:$coilVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinxSerializationJsonVersion"

    // UI
    implementation "com.afollestad.material-dialogs:core:$materialDialogsVersion"
    implementation "com.afollestad.material-dialogs:input:$materialDialogsVersion"
    implementation "me.toptas.fancyshowcase:fancyshowcaseview:$fancyShowcaseView"
}

apply plugin: 'com.google.gms.google-services'
